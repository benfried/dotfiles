{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "(ql:quickload '(:alexandria :png :iterate :flexi-streams :lparallel :cl-cpus))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "(setf lparallel:*kernel* (lparallel:make-kernel (cpus:get-number-of-processors)))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "(defun julia-count (z c &key (max 255))\n",
    "  (do ((i 0 (1+ i))\n",
    "       (p z (+ (expt p 2) c)))\n",
    "      ((or (= i max)\n",
    "           (> (abs p) 2))\n",
    "        i)))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "(defclass julia-widget (jupyter-widgets:grid-box)\n",
    "  ((image\n",
    "     :reader julia-image\n",
    "     :initform (make-instance 'jupyter-widgets:image\n",
    "                              :width 640 :height 640\n",
    "                              :layout (make-instance 'jupyter-widgets:layout :grid-area \"image\")))\n",
    "   (frame\n",
    "     :reader julia-frame\n",
    "     :initform (png:make-image 640 640 1 8))\n",
    "   (x\n",
    "     :reader julia-x\n",
    "     :initform (make-instance 'jupyter-widgets:float-text\n",
    "                              :value 0 :description \"x\"\n",
    "                              :layout (make-instance 'jupyter-widgets:layout :grid-area \"x\")))\n",
    "   (y\n",
    "     :reader julia-y\n",
    "     :initform (make-instance 'jupyter-widgets:float-text\n",
    "                              :value 0 :description \"y\"\n",
    "                              :layout (make-instance 'jupyter-widgets:layout :grid-area \"y\")))\n",
    "   (size\n",
    "     :reader julia-size\n",
    "     :initform (make-instance 'jupyter-widgets:float-text\n",
    "                              :value 4 :description \"size\"\n",
    "                              :layout (make-instance 'jupyter-widgets:layout :grid-area \"size\")))\n",
    "   (ca\n",
    "     :reader julia-ca\n",
    "     :initform (make-instance 'jupyter-widgets:float-text\n",
    "                              :value -0.8 :step 0.001 :description \"ca\"\n",
    "                              :layout (make-instance 'jupyter-widgets:layout :grid-area \"ca\")))\n",
    "   (cb\n",
    "     :reader julia-cb\n",
    "     :initform (make-instance 'jupyter-widgets:float-text\n",
    "                              :value 0.156 :step 0.001 :description \"cb\"\n",
    "                              :layout (make-instance 'jupyter-widgets:layout :grid-area \"cb\")))\n",
    "   (progress\n",
    "     :reader julia-progress\n",
    "     :initform (make-instance 'jupyter-widgets:int-progress\n",
    "                              :description \"Progress\"\n",
    "                              :max 640\n",
    "                              :layout (make-instance 'jupyter-widgets:layout :grid-area \"progress\")))\n",
    "   (task-channel\n",
    "     :reader julia-task-channel\n",
    "     :initform (lparallel:make-channel)))\n",
    "  (:metaclass jupyter-widgets:trait-metaclass)\n",
    "  (:default-initargs\n",
    "    :layout (make-instance 'jupyter-widgets:layout\n",
    "                           :grid-gap \".25em\"\n",
    "                           :grid-template-columns \"1fr min-content\"\n",
    "                           :grid-template-rows \"min-content min-content min-content min-content min-content min-content 1fr\"\n",
    "                           :grid-template-areas \"\\\"image x\\\" \\\"image y\\\" \\\"image size\\\" \\\"image ca\\\" \\\"image cb\\\" \\\"image progress\\\" \\\"image .\\\"\")))\n",
    "\n",
    "\n",
    "(defun calculate-row (frame y c width height xmin xmax ymin ymax)\n",
    "  (let ((zy (+ ymin (* (coerce (/ y height) 'float) (- ymax ymin)))))\n",
    "    (dotimes (x width)\n",
    "      (setf (aref frame y x 0)\n",
    "            (- 255 (julia-count (complex (+ xmin (* (coerce (/ x width) 'float) (- xmax xmin))) zy) c))))))\n",
    "\n",
    "\n",
    "(defun update (instance)\n",
    "  (bordeaux-threads:make-thread\n",
    "    (lambda ()\n",
    "      (with-slots (task-channel image frame x y size ca cb progress) instance\n",
    "        (let* ((c (complex (jupyter-widgets:widget-value ca)\n",
    "                           (jupyter-widgets:widget-value cb)))\n",
    "               (x-value (jupyter-widgets:widget-value x))\n",
    "               (y-value (jupyter-widgets:widget-value y))\n",
    "               (size-value (jupyter-widgets:widget-value size))\n",
    "               (width (jupyter-widgets:widget-width image))\n",
    "               (height (jupyter-widgets:widget-height image))\n",
    "               (xmin (- x-value (/ size-value 2)))\n",
    "               (xmax (+ x-value (/ size-value 2)))\n",
    "               (ymin (- y-value (/ size-value 2)))\n",
    "               (ymax (+ y-value (/ size-value 2))))\n",
    "          (dotimes (y height)\n",
    "            (lparallel:submit-task task-channel #'calculate-row frame y c width height xmin xmax ymin ymax))\n",
    "          (dotimes (y height)\n",
    "            (lparallel:receive-result task-channel)\n",
    "            (setf (jupyter-widgets:widget-value progress) y))\n",
    "          (setf (jupyter-widgets:widget-value image)\n",
    "                (flexi-streams:with-output-to-sequence (o)\n",
    "                                                       (png:encode frame o))))))))\n",
    "\n",
    "\n",
    "\n",
    "(defmethod initialize-instance :after ((instance julia-widget) &rest initargs &key &allow-other-keys)\n",
    "  (declare (ignore initargs))\n",
    "  (jupyter-widgets:observe (julia-x instance) :value\n",
    "                           (lambda (inst type name old-value new-value source)\n",
    "                             (declare (ignore inst type name old-value new-value source))\n",
    "                             (update instance)))\n",
    "  (jupyter-widgets:observe (julia-y instance) :value\n",
    "                           (lambda (inst type name old-value new-value source)\n",
    "                             (declare (ignore inst type name old-value new-value source))\n",
    "                             (update instance)))\n",
    "  (jupyter-widgets:observe (julia-size instance) :value\n",
    "                           (lambda (inst type name old-value new-value source)\n",
    "                             (declare (ignore inst type name old-value new-value source))\n",
    "                             (update instance)))\n",
    "  (jupyter-widgets:observe (julia-ca instance) :value\n",
    "                           (lambda (inst type name old-value new-value source)\n",
    "                             (declare (ignore inst type name old-value new-value source))\n",
    "                             (update instance)))\n",
    "  (jupyter-widgets:observe (julia-cb instance) :value\n",
    "                           (lambda (inst type name old-value new-value source)\n",
    "                             (declare (ignore inst type name old-value new-value source))\n",
    "                             (update instance)))\n",
    "  (setf (jupyter-widgets:widget-children instance)\n",
    "        (list (julia-image instance)\n",
    "              (julia-x instance)\n",
    "              (julia-y instance)\n",
    "              (julia-size instance)\n",
    "              (julia-ca instance)\n",
    "              (julia-cb instance)\n",
    "              (julia-progress instance)))\n",
    "  (update instance))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "(make-instance 'julia-widget)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Common Lisp (SBCL)",
   "language": "common-lisp",
   "name": "common-lisp_sbcl"
  },
  "language_info": {
   "codemirror_mode": "text/x-common-lisp",
   "file_extension": ".lisp",
   "mimetype": "text/x-common-lisp",
   "name": "common-lisp",
   "pygments_lexer": "common-lisp",
   "version": "2.0.8"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
